# https://github.com/meemknight/cmakeSetup
# Version 1.1.0

cmake_minimum_required(VERSION 3.20)

project(PhysicsEngine)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

#! ! ! ! ! ! !
#set this to ON instead of OFF to ship the game!
#! ! ! ! ! ! !
set(PRODUCTION_BUILD OFF CACHE BOOL "Make this a production build" FORCE)

# Proper MSVC runtime (static runtime)
set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")

# Interprocedural optimization (LTO)
if(PRODUCTION_BUILD)
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
else()
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION FALSE)
endif()

# SIMD optimization
if(MSVC)
    add_compile_options(/arch:AVX2)
endif()

# ===============================
# Third Party Libraries
# ===============================

set(GLFW_BUILD_DOCS OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_TESTS OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(GLFW_INSTALL OFF CACHE BOOL "" FORCE)
set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)

add_subdirectory(thirdparty/glfw-3.3.2)
add_subdirectory(thirdparty/glad)
add_subdirectory(thirdparty/stb_image)
add_subdirectory(thirdparty/stb_truetype)
add_subdirectory(thirdparty/raudio)
add_subdirectory(thirdparty/glm)
add_subdirectory(thirdparty/imgui-docking)

# ===============================
# Source Files
# ===============================

file(GLOB_RECURSE MY_SOURCES CONFIGURE_DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp"
)

add_executable(${CMAKE_PROJECT_NAME})

target_sources(${CMAKE_PROJECT_NAME}
    PRIVATE
        ${MY_SOURCES}
        src/Rendering/Renderer.cpp
        include/Rendering/Renderer.h
        include/Rendering/VertexBuffer.h
        src/Rendering/VertexBuffer.cpp
        include/Rendering/VertexArray.h
        src/Rendering/VertexArray.cpp
        include/Rendering/VertexBufferLayout.h
        include/Rendering/Shader.h
        src/Rendering/Shader.cpp
        src/Game/PhysicsEngine.cpp
        src/Physics/PhysicsLayer.cpp
        include/Physics/PhysicsLayer.h
)

set_property(TARGET ${CMAKE_PROJECT_NAME} PROPERTY CXX_STANDARD 17)

target_compile_definitions(${CMAKE_PROJECT_NAME} PUBLIC GLFW_INCLUDE_NONE=1)

# ===============================
# Production vs Development
# ===============================

if(PRODUCTION_BUILD)
    target_compile_definitions(${CMAKE_PROJECT_NAME} PUBLIC
        RESOURCES_PATH="./resources/"
        PRODUCTION_BUILD=1
        DEVELOPLEMT_BUILD=0
    )
else()
    target_compile_definitions(${CMAKE_PROJECT_NAME} PUBLIC
        RESOURCES_PATH="${CMAKE_CURRENT_SOURCE_DIR}/resources/"
        PRODUCTION_BUILD=0
        DEVELOPLEMT_BUILD=1
    )
endif()

# ===============================
# MSVC Specific
# ===============================

if(MSVC)
    target_compile_definitions(${CMAKE_PROJECT_NAME} PUBLIC _CRT_SECURE_NO_WARNINGS)
    set_target_properties(${CMAKE_PROJECT_NAME} PROPERTIES
        LINK_FLAGS "/SUBSYSTEM:CONSOLE"
    )
endif()

# Force remove Unicode
if(WIN32)
    target_compile_options(${CMAKE_PROJECT_NAME} PRIVATE /UUNICODE /U_UNICODE)
endif()

# Includes
target_include_directories(${CMAKE_PROJECT_NAME}
    PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/include/"
)

# Link libraries
target_link_libraries(${CMAKE_PROJECT_NAME}
    PRIVATE
        glm
        glfw
        glad
        stb_image
        stb_truetype
        raudio
        imgui
)